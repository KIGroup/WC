<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="WC.Data.ParticipantSingle">
<Super>WC.Data.Participant</Super>
<TimeCreated>63405,55656.736112</TimeCreated>

<Property name="Athlete">
<Type>Person</Type>
<Required>1</Required>
</Property>

<Index name="CmpAthIdx">
<Properties>Competition,Athlete</Properties>
<Unique>1</Unique>
</Index>

<Method name="ConvertToProxyObject">
<FormalSpec>fullInfo:%Boolean=0</FormalSpec>
<ReturnType>%ZEN.proxyObject</ReturnType>
<Implementation><![CDATA[
	set proxy = ##class(%ZEN.proxyObject).%New()
	set proxy.id = ..%Id()
	set proxy.selfLink = "http://"_##class(Settings).GetWebAppDomain()_##class(Settings).GetWebAppAdmin()_"/participant/"_..%Id()
	set proxy.competition = ..Competition.ConvertToProxyObject()
	set proxy.athlete = ..Athlete.ConvertToProxyObject()
	set proxy.created = ..CreatedTS
	
	quit proxy
]]></Implementation>
</Method>

<Method name="Create">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[recorder:Recorder,competition:Competition,data:%RegisteredObject,*response:%RegisteredObject,&st:%Status]]></FormalSpec>
<ReturnType>ParticipantSingle</ReturnType>
<Implementation><![CDATA[
	set st = $$$OK

	set participant = $$$NULLOREF

	set athlete = ##class(WC.Data.Person).SyncWDSF(data.number, 1, .st)
	if $$$ISERR(st)	{
		set response = ##class(WC.Utils.Response).PersonSyncFailed(data.number, $SYSTEM.Status.GetErrorText(st))
		quit participant
	}
	
	// Check allowed age group for competition
	if (competition.AgeGroup.AllowedToDance.FindOref(athlete.AgeGroup) = ""){
		set st = ##class(WC.Utils.Msg).GetErrorStatus("error_Athlete_AgeGroup_NotAllowedForCompetition")
		set response = ##class(WC.Utils.Response).AthleteAgeGroupNotAllowedForCompetition(competition, athlete)
		quit participant	
	}
	
	set participant = ..%New()
	set participant.Recorder = recorder
	set participant.Competition = competition
	set participant.Athlete = athlete
	set st = participant.%Save()
	
	if $$$ISERR(st) {
		set response = ##class(WC.Utils.Response).ParticipantSingleNotCreated(competition, athlete, $SYSTEM.Status.GetErrorText(st))
	}
	
	quit participant
]]></Implementation>
</Method>

<Method name="Delete">
<ClassMethod>1</ClassMethod>
<FormalSpec>recorder:Recorder,competition:Competition,data:%RegisteredObject,*response:%RegisteredObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set st = $$$OK

	set st = ..CmpAthIdxDelete(competition.%Id(), data.number, 4)
	
	if $$$ISERR(st) {
		set response = ##class(WC.Utils.Response).ParticipantSingleNotRemoved(..CmpAthIdxOpen(competition.%Id(), data.number), $SYSTEM.Status.GetErrorText(st))
	}
	
	quit st
]]></Implementation>
</Method>

<Method name="GetByNumbers">
<ClassMethod>1</ClassMethod>
<FormalSpec>competitionId:%String,number:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[	quit ..CmpAthIdxOpen(competitionId, number)
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
/*
<participant xmlns:xlink="http://www.w3.org/1999/xlink" xmlns="http://services.worlddancesport.org/api">
<id>1469053</id>
<number>294</number>
<status>Present</status>
<basepoints>0</basepoints>
<rank>1</rank>
<competitionId>45505</competitionId>
<rounds/>
<personId>0</personId>
<name>Shane McKeever</name>
<country>Ireland, Republic of</country>
</participant>
*/
]]></Content>
</UDLText>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
<DefaultData>ParticipantSingleDefaultData</DefaultData>
<Data name="ParticipantSingleDefaultData">
<Subscript>"ParticipantSingle"</Subscript>
<Value name="1">
<Value>Athlete</Value>
</Value>
</Data>
</Storage>
</Class>
</Export>
